// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   //zero
   Not16(in=x, out=notx);
   And16(a=x, b=notx, out=zero);
   
   //zx
   Mux16(a=x, b=zero, sel=zx, out=zerox);
   
   //zy
   Mux16(a=y, b=zero, sel=zy, out=zeroy);
   
   //negative
   Not16(in=zerox, out=notzerox);
   Not16(in=zeroy, out=notzeroy);
   
   //nx
   Mux16(a=zerox, b=notzerox, sel=nx, out=convertedx);
   
   //ny
   Mux16(a=zeroy, b=notzeroy, sel=ny, out=convertedy);
   
   //x+y & x&y
   Add16(a=convertedx, b=convertedy, out=xplusy);
   And16(a=convertedx, b=convertedy, out=xandy);
   
   //f
   Mux16(a=xandy, b=xplusy, sel=f, out=fans);
   
   //notf
   Not16(in=fans, out=notfans);
   
   //no
   Mux16(a=fans, b=notfans, sel=no, out=out,
   out[15]=minus, out[0..7]=first8, out[8..15]=last8);

   //ng
   And(a=minus, b=minus, out=ng);
   
   //zr
   Or8Way(in=first8, out=o1);
   Or8Way(in=last8, out=o2);
   Not(in=o1, out=noto1);
   Not(in=o2, out=noto2);
   And(a=noto1, b=noto2, out=zr);
   
}

